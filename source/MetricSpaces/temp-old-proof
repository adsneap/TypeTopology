distance-ℚ-ℝ : (x y : ℚ) ((ε , 0<ε) : ℚ₊) → B-ℝ (ι x) (ι y) ε 0<ε → B-ℚ x y ε 0<ε
distance-ℚ-ℝ x y (ε , 0<ε) l = ∥∥-rec (ℚ<-is-prop (abs (x - y)) ε) I l
 where
  I : (Σ (p , q , u , v) ꞉ ℚ × ℚ × ℚ × ℚ , (p < x)
                                         × (u < y)
                                         × (x < q)
                                         × (y < v)
                                         × B-ℚ (min p u) (max q v) ε 0<ε)
    → abs (x - y) < ε
  I ((p , q , u , v) , p<x , u<y , x<q , y<v , l₁) = II (ℚ-trichotomous x y)
   where
    l₂ : min p u < x
    l₂ = ℚ≤-<-trans (min p u) p x (min≤ p u) p<x
    l₃ : x < max q v
    l₃ = ℚ<-≤-trans x q (max q v) x<q (max≤ q v)
    l₄ : min p u < y
    l₄ = ℚ≤-<-trans (min p u) u y (transport (_≤ u) (min-comm u p) (min≤ u p)) u<y
    l₅ : y < max q v
    l₅ = ℚ<-≤-trans y v (max q v) y<v (transport (v ≤_) (max-comm v q) (max≤ v q))
    III : (a b  : ℚ) → min p u < a → b < max q v
                     → a < b
                     → B-ℚ a b ε 0<ε
    III a b l₂ l₃ l₄ = ℚ<-trans (abs (a - b)) (abs (min p u - max q v)) ε V l₁
     where
      IV : b - a < max q v - min p u
      IV = inequality-chain-outer-bounds-inner (min p u) a b (max q v) l₂ l₄ l₃
      V : abs (a - b) < abs (min p u - max q v)
      V = transport₂ _<_ (ℚ<-abs a b l₄) (ℚ<-abs (min p u) (max q v) (ℚ<-trans₂ (min p u) a b (max q v) l₂ l₄ l₃)) IV

    II : (x < y) ∔ (x ＝ y) ∔ (y < x) → abs (x - y) < ε
    II (inl x<y) = III x y l₂ l₅ x<y
    II (inr (inl e)) = transport (_< ε) i 0<ε
     where
      i : 0ℚ ＝ abs (x - y)
      i = 0ℚ          ＝⟨ refl ⟩
          abs 0ℚ      ＝⟨ ap abs (ℚ-inverse-sum-to-zero x ⁻¹) ⟩
          abs (x - x) ＝⟨ ap (λ z → abs (x - z)) e ⟩
          abs (x - y) ∎
    II (inr (inr y<x)) = ℚ-m2 y x ε 0<ε (III y x l₄ l₃ y<x)

distance-ℚ-ℝ-ℚ : (u v : ℚ) ((ε , 0<ε) : ℚ₊) (x : ℝ) → (u < x) × (x < v) → B-ℚ u v ε 0<ε → B-ℝ (ι u) x ε 0<ε
distance-ℚ-ℝ-ℚ u v (ε , 0<ε) x (u<x , x<v) l = ∥∥-functor I (rounded-right-b (upper-cut-of x) (rounded-from-real-R x) v x<v)
 where
  I : Σ v' ꞉ ℚ , v' < v × x < v' → Σ (a , b , c , d) ꞉ ℚ × ℚ × ℚ × ℚ , (a < u) × (c < x) × (u < b) × (x < d) × B-ℚ (min a c) (max b d) ε 0<ε
  I (v' , v'<v , x<v') = (u - (v - v') , v' , u - (v - v') , v')
                       , l₁
                       , rounded-left-c (lower-cut-of x) (rounded-from-real-L x) (u - (v - v')) u l₁ u<x
                       , disjoint-from-real x u v' (u<x , x<v')
                       , x<v'
                       , transport (_< ε) (II ⁻¹) l
   where
    l₁ : u - (v - v') < u
    l₁ = ℚ<-subtraction-preserves-order u (v - v') (ℚ<-difference-positive v' v v'<v)
    II : abs (min (u - (v - v')) (u - (v - v')) - (max v' v')) ＝ abs (u - v)
    II = abs (min (u - (v - v')) (u - (v - v')) - max v' v') ＝⟨ ap₂ (λ z z' → abs (z - z')) (min-refl (u - (v - v'))) (max-refl v') ⟩
         abs (u - (v - v') - v')                             ＝⟨ ap (λ z → abs (u + z - v')) (ℚ-minus-dist v (- v') ⁻¹) ⟩
         abs (u + ((- v) + (- (- v'))) - v')                 ＝⟨ ap (λ z → abs (z - v')) (ℚ+-assoc u (- v) (- (- v')) ⁻¹) ⟩
         abs (u - v + (- (- v')) - v')                       ＝⟨ ap abs (ℚ+-assoc (u - v) ((- (- v'))) ((- v'))) ⟩
         abs (u - v + (((- (- v')) - v')))                   ＝⟨ ap (λ z → abs (u - v + z)) (ℚ-inverse-sum-to-zero' ((- v'))) ⟩
         abs (u - v + 0ℚ)                                    ＝⟨ ap abs (ℚ-zero-right-neutral (u - v)) ⟩
         abs (u - v) ∎

distance-ℚ-ℝ-ℚ' : (u v : ℚ) ((ε , 0<ε) : ℚ₊) (x : ℝ) → (u < x) × (x < v) → B-ℚ u v ε 0<ε → B-ℝ x (ι v) ε 0<ε
distance-ℚ-ℝ-ℚ' u v (ε , 0<ε) x (u<x , x<v) l = ∥∥-functor I (rounded-left-b (lower-cut-of x) (rounded-from-real-L x) u u<x)
 where
  I : Σ u' ꞉ ℚ , u < u' × u' < x → Σ (a , b , c , d) ꞉ ℚ × ℚ × ℚ × ℚ , (a < x) × (c < v) × (x < b) × (v < d) × B-ℚ (min a c) (max b d) ε 0<ε
  I (u' , u<u' , u'<x) = (u' , v + (u' - u) , u' , v + (u' - u))
                       , u'<x
                       , disjoint-from-real x u' v (u'<x , x<v)
                       , rounded-right-c (upper-cut-of x) (rounded-from-real-R x) v (v + (u' - u)) l₁ x<v
                       , l₁
                       , transport (_< ε) (II ⁻¹) l
   where
    l₁ : v < v + (u' - u)
    l₁ = ℚ<-addition-preserves-order'' v (u' - u) (ℚ<-difference-positive u u' u<u')
    II : abs (min u' u' - max (v + (u' - u)) (v + (u' - u))) ＝ abs (u - v)
    II = abs (min u' u' - max (v + (u' - u)) (v + (u' - u))) ＝⟨ ap₂ (λ z z' → abs (z - z')) (min-refl u') (max-refl (v + (u' - u))) ⟩
         abs (u' - (v + (u' - u)))                           ＝⟨ ap (λ z → abs (u' + z)) (ℚ-minus-dist v (u' - u) ⁻¹) ⟩
         abs (u' + ((- v) + (- (u' - u))))                   ＝⟨ ap (λ z → abs (u' + z)) (ℚ+-comm (- v) (- (u' - u))) ⟩
         abs (u' + ((- (u' - u)) - v))                       ＝⟨ ap abs (ℚ+-assoc u' (- (u' - u)) (- v) ⁻¹) ⟩
         abs (u' - (u' - u) - v)                             ＝⟨ ap (λ z → abs (u' + z - v)) (ℚ-minus-dist u' (- u) ⁻¹) ⟩
         abs (u' + ((- u') - (- u)) - v)                     ＝⟨ ap (λ z → abs (z - v)) ( ℚ+-assoc u' (- u') (- (- u)) ⁻¹) ⟩
         abs (u' - u' - (- u) - v)                           ＝⟨ ap (λ z → abs (z - (- u) - v)) (ℚ-inverse-sum-to-zero u' ) ⟩
         abs (0ℚ - (- u) - v)                                ＝⟨ ap (λ z → abs (z - v)) (ℚ-zero-left-neutral (- (- u))) ⟩
         abs ((- (- u)) - v)                                 ＝⟨ ap (λ z → abs (z - v)) (ℚ-minus-minus u ⁻¹) ⟩
         abs (u - v) ∎